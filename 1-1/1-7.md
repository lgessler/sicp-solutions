# Starting point

This is what we want to improve on:

```scheme
(define (improve guess x)
  (average guess (/ x guess)))

(define (average x y)
  (/ (+ x y) 2))

(define (good-enough? guess x)
  (< (abs (- (square guess) x)) 0.001))

(define (sqrt-iter guess x)
  (if (good-enough? guess x)
      guess
      (sqrt-iter (improve guess x) x)))

(define (sqrt x)
  (sqrt-iter 1.0 x))
```

# Inadequacies

For small numbers we might disagree with `good-enough?`'s judgment. For the
square root of 0.00013, it gives us (approx.) `0.00326`. `0.00326` squared is 
(approx.) 0.0000106, which is an order of magnitude off of what we started with,
0.00013. This is happening because `good-enough?` uses an arbitrary and absolute
(as opposed to proportional) error margin, `0.001`

For large numbers, the machine becomes unable to represent differences between
them with good enough precision for `good-enough?`'s absolute margin, `0.001`,
to be a possible difference between numbers of such large size. Evaluation of
`(sqrt 5000000000000000)` seems to enter an infinite loop when entered into a
REPL.

# New approach

We take the suggestion of keeping track of the proportional ("percentagewise") 
difference between this guess and the last guess and introducing an arbitrary 
percentage difference after which we will stop. We just need to modify
`good-enough?`.

```scheme
(define (good-enough? guess x)
  (< (abs (/ (- (improve guess x) guess) 
             guess)) 
     0.000001))
```

The inner division expression gives us percentage change for a pair of guesses.
We then take the absolute value of it and compare it to an arbitrary tolerance,
0.000001.

This gives us good results even for large or small numbers:

```scheme
1 ]=> (sqrt 5000000000000000)
;Value:  70710714.07307702
;Actual: 70710678.1187

1 ]=> (sqrt 0.00013)
;Value:  .011401754252637425
;Actual: .01140175425
```
